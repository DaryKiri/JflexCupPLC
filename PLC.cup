/* import the cup package */
import java_cup.runtime.*;

import plUtils.*;

/* user code, action code declarations 
action code {: 
	public static int TMP = 0;
:};
*/

/* symbols */
terminal String NUMERO, IDENT;
terminal IGUAL, MAS, MENOS, UMENOS, POR, DIV, AP, CP, PYC;
terminal PRINT, IF, ELSE;

/* conditionals symbols */
terminal EQUALS, MAYOR, MENOR, DISTINTO, MENORE, MAYORE;

/* open parenthesis and close parenthesis */
terminal APA, CPA; 

/* epsilon */

/* comprobar si todos los no terminales precisan de tener un string como valor */

non terminal String list_sent, sentencia, expresion, sentencia_simple, sentencia_comp;
non terminal String sent_print, else_if, rel_op;
non terminal IfHolder sent_if, condicion; 
non terminal s;

/* precedences */
precedence 		left	MAS, MENOS;
precedence 		left	DIV, POR;
precedence 		left	UMENOS;
precedence 		left	AP, APA, ELSE, NUMERO, IDENT, PRINT, IF;

/* grammar */
s ::= list_sent;

list_sent ::= sentencia list_sent
			  |
			  sentencia
			  ;

sentencia ::= sentencia_simple 
			  |
			  sentencia_comp
			  ;

sentencia_comp ::= APA list_sent CPA;

sentencia_simple ::= expresion:e PYC {: RESULT = e; Utils.resetTmp(); :}
			 		 |
			 		 sent_print PYC
			 		 |
			 		 sent_if
			 		 ;

sent_print ::= PRINT AP expresion:e CP {: RESULT = e; System.out.println("print " + e + ";"); Utils.resetTmp(); :};

sent_if ::= IF AP condicion:cond CP {: // para composicion con operadores logicos, cambiar este bloque a antes de condicion Inciflabel?
									System.out.println(cond.getIfLabel() + ":");
								:} 
								sentencia 
								{:
									System.out.println("goto " + cond.getFinalLabel() + ";");
									System.out.println(cond.getElseLabel() + ":");
								:} 
								else_if {: System.out.println(cond.getFinalLabel() + ":"); :} // Imprimir Final label aqui
			;

else_if ::=	ELSE sentencia 
			| 
			;

// definir en Utils la variables de operacion logica y tener un metodo en utils para distinguir ifs
rel_op ::= 	EQUALS {: RESULT = CondUtils.EQUALS; :}
			|
			DISTINTO {: RESULT = CondUtils.DISTINTO; :}
			|
			MAYOR {: RESULT = CondUtils.MAYOR; :}
			|
			MENOR {: RESULT = CondUtils.MENOR; :}
			|
			MENORE {: RESULT = CondUtils.MENORE; :}
			|
			MAYORE {: RESULT = CondUtils.MAYORE; :}
			;

// condicion tiene un objeto IfHolder para despues poder usarse en sent_if
condicion ::= expresion:e1 rel_op:op expresion:e2 
			{:	
				CondUtils.printLogical(e1, op, e2); 
				RESULT = new IfHolder();
				RESULT.setIfLabel(Utils.getCurrentLabelString());
				Utils.incrementLabel();
				RESULT.setElseLabel(Utils.getCurrentLabelString());
				Utils.incrementLabel();
				RESULT.setFinalLabel(Utils.getCurrentLabelString());
				Utils.incrementLabel();
			:} 
			  |
			  ;

expresion ::= expresion:e1 MAS expresion:e2 {: RESULT = "$t" + Utils.getTmp(); System.out.println(RESULT + " = " + e1 + " + " + e2 + ";"); :}
			 |
			 expresion:e1 MENOS expresion:e2 {: RESULT = "$t" + Utils.getTmp(); System.out.println(RESULT + " = " + e1 + " - " + e2 + ";"); :}
			 |
			 expresion:e1 POR expresion:e2 {: RESULT = "$t" + Utils.getTmp(); System.out.println(RESULT + " = " + e1 + " * " + e2 + ";"); :}
			 |
			 expresion:e1 DIV expresion:e2 {: RESULT = "$t" + Utils.getTmp(); System.out.println(RESULT + "= " + e1 + " / " + e2 + ";"); :}
			 |
			 MENOS expresion:e {: RESULT = "$t" + Utils.getTmp(); System.out.println(RESULT + " = -" + e + ";"); :}
			 %prec UMENOS
			 |
			 AP expresion:e CP {: RESULT = e; :}
			 |
			 IDENT:i IGUAL expresion:e {: RESULT = i; System.out.println(i + "=" + e + ";" ); :}
			 |
			 IDENT:i {: RESULT = i; :} 
			 |
			 NUMERO:n {: RESULT = n; :}
			 ;


